buildscript {
    ext {
        kotlin_version = '1.3.61'
        asoft_neo4j_version = "1.1.0"
        persist_version = "15.0.0"
        test_version = "4.0.0"
        android_tools_version = "3.6.0"
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$android_tools_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'kotlin-multiplatform' version '1.3.61'
}

group 'tz.co.asoft'
version '1.0.0'

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 28
    defaultConfig {
        minSdkVersion 1
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            manifest.srcFile 'src/androidMain/AndroidManifest.xml'
            resources.srcDirs = ['src/androidMain/resources']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

repositories {
    google()
    jcenter()
    maven { url "https://kotlin.bintray.com/kotlinx" }
    maven { url 'https://dl.bintray.com/kotlinx/kotlinx' }
    maven { url "https://jitpack.io" }
    maven { url "http://dl.bintray.com/kotlin/ktor" }
    maven { url "https://dl.bintray.com/korlibs/korlibs" }
    maven { url 'https://dl.bintray.com/kotlin/kotlin-js-wrappers' }
    mavenCentral()
}


kotlin {
    jvm() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }
    }

    android() {
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                jvmTarget = "1.8"
            }
        }

        publishLibraryVariants("release")
    }

    js() {
//        browser()
//        nodejs()
        compilations.all {
            tasks[compileKotlinTaskName].kotlinOptions {
                kotlinOptions.metaInfo = true
                kotlinOptions.sourceMap = true
                kotlinOptions.moduleKind = 'commonjs'
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
            }
        }

        commonTest {
            dependencies {
                implementation "com.github.andylamax.asoft-test:asoft-test-metadata:$test_version"
            }
        }

        jvmMain {
            dependencies {
                implementation kotlin('stdlib')
            }
        }

        jvmTest {
            dependencies {
                implementation "com.github.andylamax.asoft-test:asoft-test-jvm:$test_version"
            }
        }

        androidMain {
            dependencies {
                implementation kotlin('stdlib')
            }
        }

        androidTest {
            dependencies {
                implementation "com.github.andylamax.asoft-test:asoft-test-android:$test_version"
            }
        }

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }

        jsTest {
            dependsOn jsMain
            dependencies {
                implementation "com.github.andylamax.asoft-test:asoft-test-js:$test_version"
            }
        }
    }
}